   10 "A": CLEAR 
   12 INPUT "NO. OF DATA=";N
   13 LPRINT "NO. OF DATA=";N
   14 DIM X(N-1),Y(N-1),B$(0)
   70 FOR A=1 TO N
   80 B$="X("+STR$ A+")="
   81 PAUSE B$: INPUT X(A-1)
   82 LPRINT B$;X(A-1)
   95 B$="Y("+STR$ A+")="
   96 PAUSE B$: INPUT Y(A-1)
   97 LPRINT B$;Y(A-1)
  110 NEXT A
  120 END 
  130 "C": LPRINT "":I=0:J=0:K=0:L=0:M=0
  150 FOR A=1 TO N
  155 Z=A-1
  160 I=I+X(Z)
  170 J=J+Y(Z)
  180 K=K+X(Z)*X(Z)
  190 L=L+X(Z)*Y(Z)
  200 M=M+Y(Z)*Y(Z)
  250 NEXT A
  260 I=I/N:J=J/N
  270 K=K-N*I*I
  280 L=L-N*I*J
  290 M=M-N*J*J
  305 H=âˆš(K*M)
  307 H=L/H
  310 LPRINT "COVARIANCE=": LPRINT L/(N-1)
  320 LPRINT "CORRELATION COEFFICIENT=": LPRINT H
  330 LPRINT "*REGRESSION COEFFICIENT*"
  340 S=L/K:T=J-S*I
  350 LPRINT " A=";S
  360 LPRINT " B=";T
  362 LPRINT "*MEAN VALUE*"
  364 LPRINT " X=";I
  366 LPRINT " Y=";J
  370 END 
  500 "B":B$(0)="": INPUT "REVISION POSITION=";B$(0)
  505 IF B$(0)="" GOTO 560
  510 V=LEN B$:D$=LEFT$ (B$(0),1):U=VAL (MID$ (B$(0),3,V-1))
  515 IF (U<=0)+(U>N)=1 GOTO 500
  520 IF D$="X" INPUT "REVISION VALUE=";X(U-1): LPRINT "X(";STR$ U;")=";X(U-1): GOTO 500
  530 IF D$="Y" INPUT "REVISION VALUE=";Y(U-1): LPRINT "Y(";STR$ U;")=";Y(U-1): GOTO 500
  550 GOTO 500
  560 O$="": INPUT "ALL DATA PRINT ?(Y/N)";O$
  570 IF (O$="Y")+(O$="N")<>1 GOTO 560
  580 IF O$="N" END 
  590 LPRINT "NO. OF DATA=";N
  600 FOR I=1 TO N
  610 LPRINT "X(";STR$ I;")=";X(I-1)
  620 LPRINT "Y(";STR$ I;")=";Y(I-1)
  630 NEXT I
  640 END 
  780 "D": LPRINT "": LPRINT "*ESTIMATION*"
  785 INPUT "ESTIMATION X=";X: GOTO 800
  790 END 
  800 LPRINT " X=";X
  810 Y=S*X+T
  820 LPRINT " Y=";Y
  830 GOTO 785
