   10 "A": RANDOM : WAIT 250: PRINT "** BUGHUNT GAME **": BEEP 3
   20 X=0:Y=0:E=100:F=100:T=0:S=0
   30 A=RND 9:B=RND 9
   40 L=ABS (X-A)+ABS (Y-B)
   50 IF X=A AND Y=B GOTO 400
  100 IF L=1 BEEP 3
  110 IF L=2 BEEP 2
  120 IF L=3 BEEP 1
  130 WAIT 50: PRINT "(";STR$ (X);",";STR$ (Y);") DISTANCE=";STR$ (L);" E=";STR$ (E)
  150 S=S+1:E=F-INT (S/2)
  153 IF E<=0 THEN 500
  155 G$=INKEY$ : IF G$="" GOTO 130
  157 BEEP 1
  160 IF G$="2" LET Y=Y-1: GOTO 210
  170 IF G$="4" LET X=X-1: GOTO 210
  180 IF G$="6" LET X=X+1: GOTO 210
  190 IF G$="8" LET Y=Y+1: GOTO 210
  200 GOTO 150
  210 IF X<0 LET X=0: GOTO 150
  220 IF Y<0 LET Y=0: GOTO 150
  230 IF X>9 LET X=9: GOTO 150
  240 IF Y>9 LET Y=9: GOTO 150
  250 IF X=A AND Y=B GOTO 400
  260 E=F-INT (S/2)
  270 IF E<=0 GOTO 500
  280 R=RND 5
  290 IF R=1 LET B=B-1: GOTO 340
  300 IF R=2 LET A=A-1: GOTO 340
  310 IF R=3 LET A=A+1: GOTO 340
  320 IF R=4 LET B=B+1: GOTO 340
  340 IF A<0 OR A>9 GOTO 370
  350 IF B<0 OR B>9 GOTO 370
  360 GOTO 40
  370 BEEP 4: PAUSE "*** WARP ***": GOTO 30
  400 PAUSE "HIT! HIT!"
  410 BEEP 5
  420 PAUSE "BANG!  BANG!"
  430 T=T+1:C=RND 3*5:F=F+C
  435 E=F-INT (S/2)
  440 WAIT 100: PRINT "SCORE ";T;" ENERGY ";E
  450 GOTO 30
  500 WAIT : PRINT "SCORE ";STR$ (T);" *GAME OVER!!*"
  510 END 
