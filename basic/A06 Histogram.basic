   10 CLEAR : WAIT 100
   20 PRINT "RANGE  A =< DATA =< B": INPUT "A=";A,"B=";B
   30 INPUT "SCALE UNIT SIZE=";D
   40 C=B-A:E=INT (C/D)
   60 DIM D(E),B$(0)*24
   70 LPRINT "INPUT DATA"
   80 K=1: WAIT 0:F=0
   90 "A" USING : IF F>0 LET F=0: LPRINT "": LPRINT "INPUT DATA"
  100 X=-€99
  110 INPUT "DATA=";X
  120 IF (X<A)+(X>B)=1 GOTO 100
  125 LPRINT "DATA=  ";X
  130 N=INT ((X-A)/D)
  140 D(N)=D(N)+1
  150 U=U+X:V=V+X*X
  160 K=K+1
  170 GOTO 100
  200 "B" USING : IF F<>1 LET F=1: LPRINT "": LPRINT "DELETED DATA"
  210 INPUT "DELETE DATA=";X
  230 IF (X<A)+(X>B)=1 GOTO 210
  235 LPRINT "**  ";X
  240 N=INT ((X-A)/D)
  250 IF D(N)-1<0 BEEP 2: WAIT : PRINT "OVER-DELETED CHECK"
  260 D(N)=D(N)-1:K=K-1
  265 U=U-X:V=V-X*X
  270 GOTO 210
  300 "C"F=2: WAIT 100
  305 LPRINT "": LPRINT "": LPRINT "HISTOGRAM"
  310 P=1: INPUT "*= ";P: LPRINT "*=";P
  320 LPRINT ""
  330 LPRINT " ";USING "#####";0;5*P;10*P;15*P
  340 LPRINT "     +----+----+----+---"
  345 H=A
  350 FOR L=0 TO E
  360 IF D(L)=0 GOTO 440
  370 Q=INT (D(L)/P): IF D(L)>Q*P LET Q=Q+1
  380 IF Q>18 LET Q=18
  390 B$(0)="     I"
  400 FOR M=1 TO Q:B$(0)=B$(0)+"*": NEXT M
  410 LPRINT USING "#####";H;"I             ";D(L)
  420 LPRINT B$(0)
  430 GOTO 460
  440 LPRINT USING "#####";H;"I"
  450 LPRINT "     I"
  460 H=H+D: NEXT L
  470 LPRINT USING "#####";B;"+----+----+----+---"
  480 LPRINT "": LPRINT ""
  500 W=U/(K-1): USING 
  505 LPRINT "NO. OF DATA= ";K-1
  510 LPRINT "MEAN VALUE= ";W
  520 LPRINT "STD. DEV.= ";√(-W*W+V/(K-1))
  530 END 
